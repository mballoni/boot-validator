buildscript {
    ext {
        def release = project.properties['releaseVersion'] ?: 'SNAPSHOT'
        applicationVersion = '0.0.1-' + release
    }
}

plugins {
    id 'java'
    id "io.spring.dependency-management" version "1.0.10.RELEASE" apply false
    id "org.sonarqube" version "3.0" apply false
    id "jacoco"
    id "org.owasp.dependencycheck" version "6.0.1" apply false
    id 'org.springframework.boot' version '2.3.3.RELEASE' apply false
}

apply from: rootProject.file('gradle/install-git-hooks.gradle')

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    group = 'br.com.mballoni'
    sourceCompatibility = '11'
    version = "${applicationVersion}"
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.sonarqube"
    apply plugin: "jacoco"
    apply plugin: "org.owasp.dependencycheck"
    apply plugin: 'maven-publish'
    apply plugin: 'checkstyle'

    ext {
        set('springBootVersion', '2.3.3.RELEASE')
    }

    repositories {
        maven { url "https://repo.spring.io/libs-release" }
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    sonarqube {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.projectName", "Boot Validator"
            property "sonar.organization", "$System.env.SONAR_ORG"
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.login", "$System.env.SONAR_TOKEN"
            property "sonar.java.checkstyle.reportPaths", "build/reports/checkstyle/main.xml,build/reports/checkstyle/test.xml"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
        useJUnitPlatform()
    }

    checkstyle {
        showViolations false
        toolVersion '8.34'
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled true
            html.enabled true
        }
    }
}

jar.enabled = false

tasks.register("configureLinuxIdea")
        {
            println "Configuring Linux Idea"
            outputs.upToDateWhen { false }

            def home = System.properties['user.home']

            file("${home}/.config/JetBrains/").eachDir() { dir ->
                def target = new File("${home}/.config/JetBrains/${dir.name}/codestyles/intellij-java-google-style.xml")

                new URL('https://raw.githubusercontent.com/google/styleguide/gh-pages/intellij-java-google-style.xml')
                        .withInputStream { i -> target.withOutputStream { it << i } }
            }

            copy {
                from '.intellij/checkstyle'
                into ".idea/codeStyles"
            }
        }
