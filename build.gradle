buildscript {
    ext {
        def release = project.properties['releaseVersion'] ?: 'SNAPSHOT'
        applicationVersion = '0.0.1-' + release
    }
}

plugins {
    id 'java'
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
    id "org.sonarqube" version "2.8" apply false
    id "jacoco"
    id "org.owasp.dependencycheck" version "5.3.0" apply false
    id 'org.springframework.boot' version '2.2.4.RELEASE' apply false
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    group = 'br.com.mballoni'
    sourceCompatibility = '11'
    version = "${applicationVersion}"
}

subprojects {
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "org.sonarqube"
    apply plugin: "jacoco"
    apply plugin: "org.owasp.dependencycheck"
    apply plugin: "maven"
    apply plugin: 'maven-publish'

    ext {
        set('springBootVersion', '2.2.3.RELEASE')
    }

    repositories {
        maven { url "http://repo.spring.io/libs-release" }
        mavenCentral()
        mavenLocal()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        }
    }

    sonarqube {
        properties {
            property "sonar.sourceEncoding", "UTF-8"
            property "sonar.projectName", "Boot Validator"
            property "sonar.organization", "$System.env.SONAR_ORG"
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.login", "$System.env.SONAR_TOKEN"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
        }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
}

jar.enabled = false
